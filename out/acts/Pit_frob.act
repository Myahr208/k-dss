behaviour frob of Pit
interface frob(bytes32 ilk, int256 dink, int256 dart)

types

    Can    : uint256
    Live   : uint256
    Line   : uint256
    Vat    : address VatLike
    Spot   : uint256
    Line_i : uint256
    Ink_i  : uint256
    Art_i  : uint256
    Ink_iu  : uint256
    Art_iu  : uint256
    Take   : uint256
    Rate   : uint256
    Gem_u  : uint256
    Dai    : uint256
    Debt   : uint256

storage

    live           |-> Live
    Line           |-> Line
    vat            |-> Vat
    ilks[ilk].line |-> Line_i
    ilks[ilk].spot |-> Spot

storage Vat

    wards[ACCT_ID]           |-> Can
    ilks[ilk].rate           |-> Rate
    ilks[ilk].take           |-> Take
    ilks[ilk].Ink            |-> Ink_i  => Ink_i + dink
    ilks[ilk].Art            |-> Art_i  => Art_i + dart
    urns[ilk][CALLER_ID].ink |-> Ink_iu  => Ink_iu + dink
    urns[ilk][CALLER_ID].art |-> Art_iu  => Art_iu + dart
    gem[ilk][CALLER_ID]      |-> Gem_u  => Gem_u - Take * dink
    dai[CALLER_ID]           |-> Dai    => Dai + Rate * dart
    debt                     |-> Debt   => Debt + Rate * dart

iff

    // act: caller is `. ? : not` authorised
    Can == 1
    // act: `Rate` is `. ? : not` non-zero
    Rate =/= 0
    // act: position is `. ? : not` either below collateral and global ceiling or is dai-decreasing
    (((((Art_iu + dart) * Rate) <= (#Ray * Spot)) and (((Debt + (Rate * dart))) < (#Ray * Line))) or (dart <= 0))
    // act: position is `. ? : not` either safe or risk-decreasing
    (((dart <= 0) and (dink >= 0)) or (((Ink_iu + dink) * Spot) >= ((Art_iu + dart) * Rate)))
    // act: system is `. ? : not` live
    Live == 1
    // act: call stack is not too big
    VCallDepth < 1024

iff in range uint256

    Ink_i + dink
    Art_i + dart
    Ink_iu + dink
    Art_iu + dart
    Gem_u - Take * dink
    Dai + Rate * dart
    Debt + Rate * dart
    (Art_iu + dart) * Rate
    (Ink_iu + dink) * Spot
    #Ray * Spot
    #Ray * Line

iff in range int256

    Take
    Take * dink
    Rate
    Rate * dart

if

    VGas > 300000
