behaviour grab of Vat
interface grab(bytes32 i, bytes32 u, bytes32 v, bytes32 w, int256 dink, int256 dart)

types

    Can    : uint256
    Take   : uint256
    Rate   : uint256
    Ink_iu : uint256
    Art_iu : uint256
    Ink_i  : uint256
    Art_i  : uint256
    Gem_iv : uint256
    Sin_w  : uint256
    Vice   : uint256

storage

    #Vat.wards[CALLER_ID] |-> Can
    #Vat.ilks[i].take     |-> Take
    #Vat.ilks[i].rate     |-> Rate
    #Vat.urns[i][u].ink   |-> Ink_iu => Ink_iu + dink
    #Vat.urns[i][u].art   |-> Art_iu => Art_iu + dart
    #Vat.ilks[i].Ink      |-> Ink_i  => Ink_i  + dink
    #Vat.ilks[i].Art      |-> Art_i  => Art_i  + dart
    #Vat.gem[i][v]        |-> Gem_iv => Gem_iv - (Take * dink)
    #Vat.sin[w]           |-> Sin_w  => Sin_w  - (Rate * dart)
    #Vat.vice             |-> Vice   => Vice   - (Rate * dart)

iff

    Can == 1

iff in range uint256

    Ink_iu + dink
    Art_iu + dart
    Ink_i  + dink
    Art_i  + dart
    Gem_iv - (Take * dink)
    Sin_w  - (Rate * dart)
    Vice   - (Rate * dart)

iff in range int256

    Take
    Rate
    Take * dink
    Rate * dart

if

    VGas > 300000
