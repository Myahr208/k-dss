behaviour flap of Vow
interface flap()

types

    Cow      : address Flappy
    Vat      : address VatLike
    Bump     : uint256
    Hump     : uint256
    Ash      : uint256
    DaiMove  : address DaiMoveLike
    Could    : uint256
    Bid_was  : uint256
    Lot_was  : uint256
    Guy_was  : address
    Tic_was  : uint48
    End_was  : uint48
    Gal_was  : address
    Ttl      : uint48
    Tau      : uint48
    Kicks    : uint256
    Can_move : uint256
    Dai_v    : uint256
    Sin_v    : uint256
    Dai_c    : uint256

storage

    cow  |-> Cow
    vat  |-> Vat
    bump |-> Bump
    hump |-> Hump
    Sin  |-> Ssin
    Ash  |-> Ash

storage DaiMove

    vat                         |-> Vat
    can[ACCT_ID][Cow]           |-> Could => 0

storage Cow

    #Flap.dai                         |-> DaiMove
    #Flap.ttl_tau                     |-> #WordPackUInt48UInt48(Ttl, Tau)
    #Flap.kicks                       |-> Kicks   => 1 + Kicks
    #Flap.bids[1 + Kicks].bid         |-> Bid_was => 0
    #Flap.bids[1 + Kicks].lot         |-> Lot_was => Bump
    #Flap.bids[1 + Kicks].guy_tic_end |-> #WordPackAddrUInt48UInt48(Guy_was, Tic_was, End_was) => #WordPackAddrUInt48UInt48(ACCT_ID, Tic_was, TIME + Tau)
    #Flap.bids[1 + Kicks].gal         |-> Gal_was => ACCT_ID

storage Vat

    wards[DaiMove] |-> Can_move
    dai[ACCT_ID]   |-> Dai_v => Dai_v - #Ray * Bump
    sin[ACCT_ID]   |-> Sin_v
    dai[Cow]       |-> Dai_c => Dai_c + #Ray * Bump

iff

    // doc: there is enough `Joy`
    Dai_v / #Ray >= (Sin_v + Bump) + Hump
    // doc: there is no `Woe`
    (Sin_v / #Ray - Ssin) - Ash == 0
    // doc: DaiMove is authorised to call Vat
    Can_move == 1
    // act: call stack is not too big
    VCallDepth < 1022

iff in range uint48

    TIME + Tau

iff in range uint256

    Sin_v + Bump
    Sin_v / #Ray - Ssin
    (Sin_v / #Ray - Ssin) - Ash
    1 + Kicks
    Dai_v - #Ray * Bump
    Dai_c + #Ray * Bump

iff in range int256

     #Ray * Bump

if

    Cow =/= DaiMove
    Cow =/= Vat
    Vat =/= DaiMove
    VGas > 600000

returns 1 + Kicks
